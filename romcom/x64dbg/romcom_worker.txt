// ROMCOM 3.0 worker component x64dbg script
// This script does two things for ROMCOM 3.0 worker samples: it
// sets a breaktpoint at the instruction that handles the command
// sent by the C2 and patches the listening address so we can
// interact with it from anywhere
//
// Author: Fernando MercÃªs @ Trend Micro FTR Team
// Date: 2023-03-23

// Set the command-line to execute the Main function as the malware does
setcmdline "C:\Windows\System32\rundll32.exe C:\Users\user\Desktop\winipfile3642346241.dll0,Main winipfile3642346241.dll0"

// Disable SystemBreakpoint
config Events, SystemBreakpoint, 0

// Enable break on DLL load, so we break when rundll32.exe loads our malicious DLL
config Events, DllLoad, 1
run
zzz .1000

// Now the DLL is loaded, let's break at main. We need a hardware breakpoint to avoid exceptions.
bph Main
config Events, DllLoad, 0
erun

// Put a break point at the jmp <command sent> instruction
find mem.base(Main), FF E1 48 8D 8D E0 39 00 00 E8 9E 3F 01 00 90
bp $result

// We break at bind() so we can patch the address the malware listen for commands (originally 127.0.0.1:5555)
bp bind
run
// Set the address to 0.0.0.0. We leave the port unchanged.
4:[rdx+4] = 0

run


